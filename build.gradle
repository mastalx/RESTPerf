apply plugin: 'java'
apply plugin: 'application'
apply from: "gradle/jupiter.gradle"


group = 'ch.tie'

dependencies {
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.7.4'
	compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'

	compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.6.1'
	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.6.1'
	compile group: 'commons-io', name: 'commons-io', version: '2.5'

	compile group: 'org.springframework.hateoas', name: 'spring-hateoas', version: '0.23.0.RELEASE'

	compile group: 'commons-cli', name: 'commons-cli', version: '1.4'

	testCompile 'org.hamcrest:hamcrest-core:1.3'
}

repositories {
	mavenCentral()
}

application {
	mainClassName = 'ch.tie.perf.WadoPerfMain'
	applicationName = 'WadoPerf'
}

task createRESTPerfApp(type: CreateStartScripts) {
	mainClassName = 'ch.tie.perf.RestPerfMain'
	classpath = startScripts.classpath
	outputDir = startScripts.outputDir
	applicationName = 'RestPerf'
}

applicationDistribution.into("bin") {
	duplicatesStrategy= DuplicatesStrategy.EXCLUDE
	from(createRESTPerfApp)
	fileMode = 0755
}